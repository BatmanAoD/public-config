" TODO loooooots of this doesn't work with Vi (`vim-gtk` or similar must be
" installed). This is annoying when setting up a new machine/profile/etc.
"
" TODO: Look through some .vimrc files made by people who actually know what
" they're doing:
" * https://github.com/tpope/tpope/blob/master/.vimrc
" * https://github.com/garybernhardt/dotfiles/blob/master/.vimrc
" * https://github.com/nelstrom/dotfiles/blob/master/vimrc
" * https://bitbucket.org/sjl/dotfiles/src/1da770d23a2168f0e0c2e50d0d3e84e5c6d38d27/vim/vimrc?at=default&fileviewer=file-view-default

" Get rid of any existing mappings. (Must happen BEFORE trying to apply plugin
" mappings!!!!)
mapclear

" Trying to use the Git Bash shell has a weird problem--the version of Vim
" included with Git treats (escaped) spaces in the shell variable just fine,
" but native Vim/gVim tries to execute 'C:\Program' as a program. Bleargh.
if has('win32')
    set shell=$COMSPEC
endif

" This screws up the nvim-qt rtp setup.
if !has('nvim')
    " make sure runtimepath has default value
    set rtp&
endif

" create a variable to generically reference the location of vim files
let $VIMFILES=split(&rtp,",")[0]

function! EscapeFilename(fname)
    let fname = fnameescape(expand(a:fname))
    if !filereadable(fname)
        " On windows, `fnameescape` doesn't work.
        let fname = expand(a:fname)
    endif
    " Either we successfully escaped the name, or we didn't.
    return fname
endfunction

" Setup plugin manager and load plugins
let plugin_mgr = EscapeFilename($VIMFILES . '/autoload/plug.vim')
let plug_listfile = EscapeFilename("~/.vimrcpluglist")
if filereadable(plugin_mgr) && filereadable(plug_listfile)
    exec ":source " . plug_listfile
endif

" Colors and mouse settings (use jellybeans only if it's loaded as plugin;
" jellybeans only looks good when rich colors are available, which is true for
" gvim and nvim but not console-vim)
if has('gui_running') || has('nvim')
    try
        colors jellybeans
    catch
        colors torte
    endtry
        set enc=utf-8
        set mouse=a
else
    colors torte
    set mouse=
endif

" These should be the default, right?
" set noerrorbells t_vb=

" TODO: there's a fair amount of 'trivial' setup here. Consider using this:
" https://github.com/tpope/vim-sensible
set hidden
syntax enable
set hlsearch
set nocompatible
set nostartofline
set showcmd
set ruler
set wildmenu
set wildmode=longest,list
" During macro execution, don't redraw the screen.
set lazyredraw
" Disable modelines, which have a security vulnerability and, in hindsight,
" are generally just...weird.
set nomodeline
" Automatically change to directory of current file
set autochdir
" Automatic indent
filetype plugin indent on
" All indentation levels should be rounded to a multiple of shiftwidth
set shiftround

" 'virtual' shenanigans:
" switch back to 'block' if this is too open-ended.
" alternatively, only set to 'all' if I'm editing a file with tabs.
" This would be easy since I already have the 'PickTabUsage' function.
function! TempNonVirtual()
    let g:oldvirtualedit=&virtualedit
    set virtualedit=
endfunction
" TODO: is there a better way to clean up than using two separate functions
" that must always be used in conjunction?
function! RestoreVirtual()
    let &virtualedit=g:oldvirtualedit
endfunction
set virtualedit=all
nnoremap <silent> <LeftMouse> :call TempNonVirtual()<CR><LeftMouse>:call RestoreVirtual()<CR>
" This works for most things, but is kind of stupid when the last character in
" the line is a tab.
nnoremap <silent> a :call TempNonVirtual()<CR>:call RestoreVirtual()<CR>a

if has('x11') || system('uname -r') =~ "microsoft"
    set clipboard=unnamedplus
else
    set clipboard=unnamed
endif

function! SetIndentWidth(spaces)
    let &tabstop=a:spaces
    let &shiftwidth=a:spaces
    let &softtabstop=a:spaces
endfunction

" TODO move file-specific settings to `ftplugin`
" (https://vim.fandom.com/wiki/Keep_your_vimrc_file_clean)
" My Cpp settings
function! CppFile()
    call SetIndentWidth(4)
    set tw=80
    set fo=tcrqnlj
endfunction

" My markdown settings
function! MarkdownFile()
    call SetIndentWidth(2)
    set syntax=markdown
endfunction

augroup filetypes
    " The number of indentation columns depends on the language, not on
    " tab usage
    " XXX why doesn't the tabstop of 4 apply to new files?
    au FileType * if &filetype == "cpp"
                   \| :call CppFile()
               \| else
                   \| :call SetIndentWidth(4)
               \| endif
    au FileType * if &filetype == "make"
                   \| :call Usetabs()
               \| endif
    " Use markdown syntax for .txt files, or files with 'README' in the names
    au FileType * if &filetype == "text" || &filetype == "markdown" || expand('%:t:r') =~ 'README'
                   \| :call MarkdownFile()
                \| endif
    au FileType * if &filetype == "scala"
                    \| :set iskeyword+=?
                \| endif
augroup END

" Quick manual indent-width control
nnoremap <Leader>i1 :call SetIndentWidth(1)<cr>
nnoremap <Leader>i2 :call SetIndentWidth(2)<cr>
nnoremap <Leader>i3 :call SetIndentWidth(3)<cr>
nnoremap <Leader>i4 :call SetIndentWidth(4)<cr>
nnoremap <Leader>i5 :call SetIndentWidth(5)<cr>
nnoremap <Leader>i6 :call SetIndentWidth(6)<cr>
nnoremap <Leader>i7 :call SetIndentWidth(7)<cr>
nnoremap <Leader>i8 :call SetIndentWidth(8)<cr>
nnoremap <Leader>i9 :call SetIndentWidth(9)<cr>

" Single ~ acts like g~ by allowing motion commands
"   (so ~l acts like ~ does with notildeop)
" Actually, this is kind of annoying, because apparently I apply ~ to a single
" letter rather frequently.
" set tildeop
set incsearch
if has('nvim')
    set inccommand=nosplit
endif
" This limit exists to limit memory consuption.
set undolevels=90000

" Note: in comma-separated font lists, the fonts are tried in order until an
" available font is found.
" Also note: ...nvim-qt doesn't actually work like this. NeoVide supports it
" correctly.
" TODO h10 is terrible on my Dell screen at Rigetti. Why?
set guifont=Fira\ Code\ Retina,DejaVu\ Sans\ Mono,Liberation\ Mono:h10
if has('win32') && !has('nvim')
    " Enable ligatures (nvim doesn't support a concept of 'renderoptions')
    set renderoptions=type:directx
endif

" Don't use this; makes file recovery impossible
" set noswapfile

set ignorecase
set smartcase
nnoremap <Leader>c :set ignorecase!<cr>

" to get a one-off single replacement, use /g option
set gdefault
" I don't use vim splits that much, but in any case...
set splitbelow
set splitright
" Disabled for most files; see below for cpp.
" set tw=80
" Delete comment leader when joining lines
" Other `fo` options for Cpp are set by CppFile
set fo+=j
set ww=h,l,<,>
" start scrolling 5 lines from edge of screen
set scrolloff=5
"
" expand tabs even when chars are shown explicitly; also show trailing spaces
" and end-of-line with 'set list'
set listchars=tab:»·,trail:¬
" legacy; default in nvim. See https://vi.stackexchange.com/a/2163/1060
set backspace=indent,eol,start
" This avoids most (...all?) of the 'press ENTER to continue' messages from
" simple operations like undo/redo.
set shortmess=aoOstT
if ! has('win32')
    cmap <silent> w!! w !sudo tee > /dev/null %
endif

set nobackup
set nu
set relativenumber

" Don't get caught off-guard by tabs
" Note that shiftwidth and softtabstop are set separately
function! Usetabs()
  " shiftwidth has to do with auto-indent & similar, NOT tabbing.
  " So keep 4 all the time.
  " Do NOT change tabstop.
  set noexpandtab
  " if I'm using tabs, LOOK AT THEM.
  set list
endfunction
" Once I've acknowledged that tabs are in use, make the colors quieter.
" Trailing spaces are in the same category as tabs are, so they'll get dimmer
" too. One way to deal with this would be to show the eol character.
" However, the eol character is still annoying, so don't bother.
" TODO: can I make the syntax color category for trailing spaces be something
" else, instead?
function! Tabcolors()
    if has('gui_running') && exists("g:jellybeans_overrides")
        let g:jellybeans_overrides.SpecialKey = {'guifg':'444444'}
        let g:jellybeans_overrides.NonText    = {'guifg':'7777CC'}
        colors jellybeans
    endif
endfunction
function! Nousetabs()
  " shiftwidth has to do with auto-indent & similar, NOT tabbing.
  " So keep 4 all the time.
  " If there are already tabs in the file, I should probably keep
  " whatever tabstop I already have. If not, then it shouldn't matter.
  set expandtab
  " still might want to see trailing spaces.
  " set nolist
endfunction
function! Nontabcolors()
    if has('gui_running') && exists("g:jellybeans_overrides")
        let g:jellybeans_overrides.SpecialKey = {'guifg':'FFFA00'}
        let g:jellybeans_overrides.NonText    = {'guifg':'444499'}
        colors jellybeans
    else
        set nolist
    endif
endfunction
function! ToggleTabs()
    if(&expandtab == 1)
        call Usetabs()
        call Tabcolors()
    else
        call Nousetabs()
        call Nontabcolors()
    endif
endfunction
nnoremap <leader>t :call ToggleTabs()<cr>
function! Untab()
  call Nousetabs()
  retab
endfunction

function! PickTabUsage()
    if &readonly || ! &modifiable
        set nolist
        set tabstop=8
    else
        if search('\t','nw') > 0
            call Usetabs()
        else
            call Nousetabs()
        endif
    endif
endfunction

" every time I change buffers or reload file, check if I need to use tabs
augroup autotabs
    autocmd BufEnter,BufRead * :call PickTabUsage()
augroup END

" exit vim when exiting last buffer
" courtesy of http://superuser.com/a/668612/199803
autocmd BufDelete * if len(filter(range(1, bufnr('$')), 'buflisted(v:val)')) == 2 | quit | endif

" Set scripts to be executable from the shell
" courtesy of https://unix.stackexchange.com/a/39995/38050
au BufWritePost * if getline(1) =~ "^#!" | if getline(1) =~ "/bin/" | silent execute "!chmod a+x <afile>" | endif | endif

" hm....is this actually a good idea?
" YES, it was
nnoremap <Space> :

" map ctrl-Y to copy into system register
nnoremap <C-Y> "+y
vnoremap <C-Y> "+y
" map ctrl-P to paste from system register
nnoremap <C-P> "+p
vnoremap <C-P> "+p

nnoremap <Leader>w :set wrap!<cr>

" quick uniquification
nnoremap <Leader>u :sort u<cr>

" edit this file without using the edit menu
nnoremap <Leader>v :e $MYVIMRC<cr>
" re-source start-up file
" TODO: make this a clean start somehow?
nnoremap <Leader>re :so $MYVIMRC<cr>

" '/asdf' works but is kind of stupid and annoying
nnoremap <Leader>h :noh<cr>

" Quickly font bigger/smaller
" TODO: this currently only works on Linux w/ DejaVu Sans Mono installed.
nnoremap <Leader>= :set guifont=DejaVu\ Sans\ Mono\ 18<cr>
nnoremap <Leader>- :set guifont=DejaVu\ Sans\ Mono\ 10<cr>

" Quickly underline the current line
nmap <Leader>l yypV<C-s>./-/<CR>k<Leader>h

" TODO: add a quick way to show carriage returns;
" see http://stackoverflow.com/a/27259548/1858225

inoremap <C-BS> <C-W>
inoremap <C-Del> <C-O>dw
nnoremap <C-BS> <C-W>
nnoremap <C-Del> <C-O>dw

" Change one letter using 'x'
nnoremap cx cl

" In insert or command mode, move normally by using Ctrl
inoremap <C-h> <Left>
inoremap <C-j> <Down>
inoremap <C-k> <Up>
inoremap <C-l> <Right>
cnoremap <C-h> <Left>
cnoremap <C-j> <Down>
cnoremap <C-k> <Up>
cnoremap <C-l> <Right>

" In visual and normal mode, Ctrl+up/down moves to prev/next line with same
" indentation (see
" http://vim.wikia.com/wiki/Move_to_next/previous_line_with_same_indentation)
"
" Jump to the next or previous line that has the same level or a lower
" level of indentation than the current line.
" exclusive (bool): true: Motion is exclusive
" false: Motion is inclusive
" fwd (bool): true: Go to next line
" false: Go to previous line
" lowerlevel (bool): true: Go to line with lower indentation level
" false: Go to line with the same indentation level
" skipblanks (bool): true: Skip blank lines
" false: Don't skip blank lines
function! NextIndent(exclusive, fwd, lowerlevel, skipblanks)
let line = line('.')
let column = col('.')
let lastline = line('$')
let indent = indent(line)
let stepvalue = a:fwd ? 1 : -1
while (line > 0 && line <= lastline)
    let line = line + stepvalue
    if ( ! a:lowerlevel && indent(line) == indent ||
        \ a:lowerlevel && indent(line) < indent)
    if (! a:skipblanks || strlen(getline(line)) > 0)
        if (a:exclusive)
        let line = line - stepvalue
        endif
        exe line
        exe "normal " column . "|"
        return
    endif
    endif
endwhile
endfunction
" Moving back and forth between lines of same or lower indentation.
nnoremap <silent> [l :call NextIndent(0, 0, 0, 1)<CR>
nnoremap <silent> ]l :call NextIndent(0, 1, 0, 1)<CR>
nnoremap <silent> [L :call NextIndent(0, 0, 1, 1)<CR>
nnoremap <silent> ]L :call NextIndent(0, 1, 1, 1)<CR>
vnoremap <silent> [l <Esc>:call NextIndent(0, 0, 0, 1)<CR>m'gv''
vnoremap <silent> ]l <Esc>:call NextIndent(0, 1, 0, 1)<CR>m'gv''
vnoremap <silent> [L <Esc>:call NextIndent(0, 0, 1, 1)<CR>m'gv''
vnoremap <silent> ]L <Esc>:call NextIndent(0, 1, 1, 1)<CR>m'gv''
onoremap <silent> [l :call NextIndent(0, 0, 0, 1)<CR>
onoremap <silent> ]l :call NextIndent(0, 1, 0, 1)<CR>
onoremap <silent> [L :call NextIndent(1, 0, 1, 1)<CR>
onoremap <silent> ]L :call NextIndent(1, 1, 1, 1)<CR>

" Easily recover from accidental deletions
" see http://vim.wikia.com/wiki/Recover_from_accidental_Ctrl-U
inoremap <c-u> <c-g>u<c-u>
inoremap <c-w> <c-g>u<c-w>

" Always show full path
nnoremap <C-g> 1<C-g>

" b/c I do like digraphs, but I'm using c-k for 'up'
inoremap <C-d> <C-k>

" b/c I don't use the default behavior anyway
nnoremap <C-k> <Up>
nnoremap <C-l> <Right>

" for single-character entry
" from http://vim.wikia.com/wiki/Insert_a_single_character
function! RepeatChar(char, count)
return repeat(a:char, a:count)
endfunction
nnoremap <silent> s :<C-U>exec "normal i".RepeatChar(nr2char(getchar()), v:count1)<CR>
nnoremap <silent> S :<C-U>exec "normal a".RepeatChar(nr2char(getchar()), v:count1)<CR>

" for consistency with D and C
nnoremap Y y$

" When `put`ing text, automatically auto-indent it
" See http://vi.stackexchange.com/q/3452/1060
" This is nice for preventing tabs from mysteriously appearing in files
" that previously had no tabs.
" However, it's terribly in yaml.
" TODO: Assign to a autocommand for formats with 'obvious' indenting rules
" (e.g. Rust, Go)
" nnoremap p pV`]=
" nnoremap P PV`]=

" For using visual mode to swap chunks of text:
" Delete one piece of text (using any kind of 'd' command), then visually
" select another piece of text, then press Ctrl-x to swap it with the last
" deleted text
" Taken from http://vim.wikia.com/wiki/Swapping_characters,_words_and_lines
vnoremap <C-X> <Esc>`.``gvP``P

" ex mode?? seriously?
nnoremap Q <nop>

" No `jk` or `kj` mapping for `Esc`, because remapping to CapsLock is more
" convenient anyway.

" Easily write and exit buffers
function! WriteAndDelete()
:w
:bd
endfunction

command! Wd :call WriteAndDelete()

" save/quitting should be deliberate, and NEVER accidental
nnoremap ZZ <nop>
nnoremap ZQ <nop>
" because I often try to quit after highlighting/selecting
vnoremap :q <Esc>:q
vnoremap :WQ <Esc>:WQ<cr>
vnoremap QQQ <Esc>:bd!<cr>
" easily close all buffers
command! WQ :wqa
command! Q :qa

" quick filewide search and replace; recursive to take advantage of 'magic'
" remapping
" TODO: should I make this non-recursive now that I'm using the 'enchanted'
" plugin?
nmap <C-s> :%s/
" quick selection search and replace
vmap <C-s> :s/

" quick word count
nnoremap <C-c> :%s///n<cr>
vnoremap <C-c> :%s///n<cr>
" Quick section-wide 'global'
vmap <C-g> :g/

function! ToggleGuiMenu()
    if(&guioptions =~# 'm')
        set guioptions-=m
        echo "menu off"
    else
        set guioptions+=m
        echo "menu on"
    endif
endfunction

function! ToggleGuiScroll()
    if(&guioptions =~# 'r')
        set guioptions-=r
        echo "scroll off"
    else
        set guioptions+=r
        echo "scroll on"
    endif
endfunction

" If file is very large, show scrollbar
function! ScrollLarge()
    if ! has("gui_running")
        return
    endif
    if line('$') > 1000
        set guioptions+=r
    else
        set guioptions-=r
    endif
endfunction

augroup guiopts
    " (hopefully) temporarily disabled, because this causes undesired
    " window-resizing sometimes
    " autocmd BufEnter * :call ScrollLarge()
    autocmd GUIEnter * nnoremap <Leader>s :call ToggleGuiScroll()<cr>
    autocmd GUIEnter * set guioptions-=m
    autocmd GUIEnter * nnoremap <Leader>m :call ToggleGuiMenu()<cr>
    autocmd GUIEnter * set guioptions-=T
    autocmd GUIEnter * set guioptions+=c
    autocmd GUIEnter * set nomousehide
augroup END

" from http://superuser.com/a/657733/199803
if !has('win32')
    augroup matchperms
        au BufRead * let b:oldfile = expand("<afile>")
        au BufWritePost * if exists("b:oldfile") | let b:newfile = expand("<afile>") 
                    \| if b:newfile != b:oldfile 
                    \| silent echo system("chmod --reference=".b:oldfile." ".b:newfile) 
                    \| endif |endif
    augroup END
endif

" from http://stackoverflow.com/a/4294176/1858225
function! s:MkNonExDir(file, buf)
    if empty(getbufvar(a:buf, '&buftype')) && a:file!~#'\v^\w+\:\/'
        let dir=fnamemodify(a:file, ':h')
        if !isdirectory(dir)
            call mkdir(dir, 'p')
        endif
    endif
endfunction
augroup BWCCreateDir
    autocmd!
    autocmd BufWritePre * :call s:MkNonExDir(expand('<afile>'), +expand('<abuf>'))
augroup END

" If multiple buffers, vsplit them all
"vert sba

" From https://github.com/joeytwiddle/rc_files/blob/c8264794527c6be685829f08f63fd8dfa2903528/.vim/plugin/joeycommands.vim#L112-131
" Runs the given Ex command and pipes the output to the given shell command.
" For example: :PipeToShell syn | grep 'Declaration'
" I considered other names: CmdOut, PipeToShell
command! -nargs=+ -complete=command PipeCmd call s:PassVimCommandOutputToShellCommand(<q-args>)

function! s:PassVimCommandOutputToShellCommand(line)
        let vim_cmd = substitute(a:line, '\s*|.*', '', '')
        let shell_cmd = substitute(a:line, '^[^|]*|\s*', '', '')
        " TODO: We could redir to a local variable, to avoid clobbering the 'l' register.
        redir @l
                silent exe vim_cmd
        redir END
        " To pipe to a shell, the only way I thought of was to put the data into a fresh buffer, and then do :w !...
        new
        normal "lP
        exe 'w !'.shell_cmd
        " Undo the paste so bwipeout can drop the buffer without complaint
        normal u
        exe "bwipeout"
endfunction

" Leave insert mode to the *right* of the final location of the insertion
" pointer
" From http://vim.wikia.com/wiki/Prevent_escape_from_moving_the_cursor_one_character_to_the_left
let CursorColumnI = 0 "the cursor column position in INSERT
autocmd InsertEnter * let CursorColumnI = col('.')
autocmd CursorMovedI * let CursorColumnI = col('.')
autocmd InsertLeave * if col('.') != CursorColumnI | call cursor(0, col('.')+1) | endif

" Useful if not using gVim
" TODO: figure out how to turn off the RelativeFocus autogroup once I've
" used this function to take manual control of this feature.
function! ToggleRelativeNumbering()
    if(&relativenumber == 1)
        set norelativenumber
    else
        set relativenumber
    endif
endfunction
nnoremap <Leader>n :call ToggleRelativeNumbering()<cr>

augroup RelativeFocus
    if has('win32unix') 
        " With XWin, 'focus lost' apparently only occurs when a *different*
        " X window gains focus. So here's a lame but hopefully good-enough
        " alternative.
        " TODO: if gWSL doesn't need this, get rid of it.
        au CursorHold * :set norelativenumber
        au CursorMoved * :set relativenumber
    else
        au FocusLost * :set norelativenumber
        au FocusGained * :set relativenumber
    endif
augroup END

" In Windows, when opening a file on a network drive,
" default to unix lineendings
" TODO: determine if this still works as expected
augroup UncDrive
    if has('win32')
        autocmd BufEnter,BufRead //* :set fileformat=unix
    endif
augroup END

" using logic from http://stackoverflow.com/a/9528322/1858225
if exists("+undofile")
    " Doesn't work if `$VIMFILES` has a space.
    " This might help...?
    " https://stackoverflow.com/a/4294176/1858225
    set undodir=$VIMFILES/undodir/
    if isdirectory(expand(&undodir)) == 0
        :silent call mkdir(expand(&undodir), '-p')
    endif
    set undofile
    set undolevels=90000 "maximum number of changes that can be undone
    set undoreload=10000 "maximum number lines to save for undo on a buffer reload
endif
